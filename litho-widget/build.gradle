apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
    }

    // TODO(#62): Re-enable abort on error.
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.all {
            // We use PowerMock now, so we need to limit the classloader reuse.
            forkEvery = 1
            maxParallelForks = 2
            jvmArgs '-Dcom.facebook.litho.is_oss=true'
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
            }
        }
    }
}

afterEvaluate {
    tasks.withType(Test) {
        it.dependsOn rootProject.copyYogaLibs
        systemProperty 'java.library.path', "${rootDir}/build/jniLibs"
        environment 'LD_LIBRARY_PATH', "${rootDir}/build/jniLibs"
        environment 'DYLD_LIBRARY_PATH', "${rootDir}/build/jniLibs"
    }
}

dependencies {
    compile project(':litho-core')
    provided project(':litho-annotations')
    annotationProcessor project(':litho-processor')

    // Annotations
    provided deps.jsr305
    provided deps.inferAnnotations

    // First-party
    compile deps.textlayoutbuilder

    // Android Support Library
    compile deps.supportCoreUi

    // Testing
    testCompile project(':litho-testing')
    testCompile deps.robolectric
    testCompile deps.soloader
    testCompile deps.powermockMockito
    testCompile deps.powermockJunit
    testCompile deps.powermockXstream
}

apply from: rootProject.file('gradle/release.gradle')

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts.add('archives', sourcesJar)
