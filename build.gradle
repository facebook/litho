buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${GRADLE_BINTRAY_PLUGIN_VERSION}"
        classpath "com.github.dcendents:android-maven-gradle-plugin:${ANDROID_MAVEN_GRADLE_PLUGIN_VERSION}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    repositories {
        jcenter()
    }
}


ext {
    minSdkVersion = 15
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        // Android support
        supportAnnotations : 'com.android.support:support-annotations:25.3.1',
        supportAppCompat   : 'com.android.support:appcompat-v7:25.3.1',
        supportCoreUi      : 'com.android.support:support-core-ui:25.3.1',
        supportRecyclerView: 'com.android.support:recyclerview-v7:25.3.1',
        // First-party
        yoga               : 'com.facebook.yoga:yoga:1.4.2',
        fresco             : 'com.facebook.fresco:fresco:1.2.0',
        soloader           : 'com.facebook.soloader:soloader:0.2.0',
        textlayoutbuilder  : 'com.facebook.fbui.textlayoutbuilder:textlayoutbuilder:1.1.0',
        stetho             : 'com.facebook.stetho:stetho:1.5.0',
        // Annotations
        jsr305             : 'com.google.code.findbugs:jsr305:3.0.1',
        inferAnnotations   : 'com.facebook.infer.annotation:infer-annotation:0.10.0.2',
        // Debugging and testing
        guava              : 'com.google.guava:guava:20.0',
        robolectric        : 'org.robolectric:robolectric:3.0',
        junit              : 'junit:junit:4.12',
        powermockReflect   : 'org.powermock:powermock-reflect:1.5.6',
        powermockMockito   : 'org.powermock:powermock-api-mockito:1.5.6',
        powermockJunit     : 'org.powermock:powermock-module-junit4-rule:1.5.6',
        powermockXstream   : 'org.powermock:powermock-classloading-xstream:1.5.6',
        mockitoCore        : 'org.mockito:mockito-core:1.9.5',
        assertjCore        : 'org.assertj:assertj-core:2.6.0',
        // Processor
        javapoet           : 'com.squareup:javapoet:1.8.0',
]

// This should hopefully only serve as a temporary measure until
// we have a proper Gradle setup for Yoga and JNI.
task copyYogaLibs(type: Copy, dependsOn: ':yoga:buckBuild') {
    from 'buck-out/gen/lib/fb/jni#default,shared/'
    from 'buck-out/gen/lib/yogajni/jni#default,shared/'
    include '*.so'
    include '*.dylib'
    include '*.dll'
    into "$buildDir/jniLibs"
}

afterEvaluate {
    tasks.withType(Test) {
        dependsOn copyYogaLibs
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
